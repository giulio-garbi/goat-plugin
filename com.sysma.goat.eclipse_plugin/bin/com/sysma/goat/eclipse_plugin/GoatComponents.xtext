grammar com.sysma.goat.eclipse_plugin.GoatComponents with org.eclipse.xtext.common.Terminals

generate goatComponents "http://www.sysma.com/goat/eclipse_plugin/GoatComponents"

import "http://www.sysma.com/goat/eclipse_plugin/GoatInfrastructure" as infr
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	{Model}
	'infrastructure' infrastructure = [infr::Infrastructure]
	(processes += (ProcessDefinitionBlk|ProcessDefinitionPar) |
	components += ComponentDefinition | 
	functions += FuncDefinition |
	environments += EnvironmentDefinition)*;

//Process

ProcessDefinition:
	ProcessDefinitionBlk|ProcessDefinitionPar
;
ProcessDefinitionBlk returns ProcessDefinition:
	'process' name=ID block = ProcessBlock
;
ProcessDefinitionPar returns ProcessDefinition:
	'process' name=ID '=' block = PDPBlock
;

PDPBlock:
	(procs += [ProcessDefinition] "|")* procs += [ProcessDefinition]
;

ProcessBlock:
	"{"
		{ProcessBlock} (statements += ProcessStatement)*
	"}"
;

ProcessStatement:
	ProcessSpawn | ProcessCall | ProcessSend | ProcessReceive | ProcessIf | ProcessSet | ProcessWaitFor | ProcessLoop
;

ProcessSpawn:
	"spawn" "(" blk = PDPBlock ")" 
;

ProcessCall:
	"call" "(" proc = [ProcessDefinition] ")"
;

ProcessSend:
	"send" "{" (msgOutParts+=Expression ("," msgOutParts+=Expression)*)? "}" "@" "("send_pred=OutputPredicate ")" 
		updates = Update (print = PrintStatement)? ";"  
;

ProcessReceive:
	ProcessReceiveSingle|ProcessReceiveMultiple
;
ProcessReceiveSingle:
	"receive" cases += ReceiveCase ";" then += EmptyProcessBlock
;
EmptyProcessBlock returns ProcessBlock:
	{ProcessBlock}
;
ProcessReceiveMultiple:
	"receive" "{"
		("case" cases += ReceiveCase ":" then += ProcessBlock)+ 
	"}"
;
ReceiveCase:
	 "(" cond = Expression ")" "{" (msgInParts+=ID ("," msgInParts+=ID)*)? "}"  
		updates = Update (spawn = ProcessSpawn)? (print = PrintStatement)? 
;

ProcessIf:
	"if" "("cond += Expression")" ProcessThenBlock
	("else""if" "("cond += Expression")" ProcessThenBlock)*
	("else" ProcessThenBlock)?
;
fragment ProcessThenBlock :
	"{" action+=(ProcessSend|ProcessReceive|ProcessSet) then += PTBThen "}"
;
PTBThen returns ProcessBlock:
	{ProcessBlock} (statements += ProcessStatement)*
;

ProcessSet:
	"set" update = Update (print = PrintStatement)? ";"
;

ProcessWaitFor:
	"waitfor" "(" cond = Expression ")" update = Update (print = PrintStatement)?";"
;

ProcessLoop:
	"loop" block = ProcessBlock
;

Update:
	{Update} ("[" attribute+=(UpdateComponentAttribute|UpdateLocalAttribute) ":=" value += Expression 
		("," attribute+=(UpdateComponentAttribute|UpdateLocalAttribute) ":=" value += Expression)* "]"
	)?
;
UpdateComponentAttribute:
	"comp""." atname = ID
;
UpdateLocalAttribute:
	"proc""." atname = ID
;

PrintStatement:
	/*PrintAllStatement|*/PrintFormattedStatement
;

PrintAllStatement:
	{PrintAllStatement} "print"
;

PrintFormattedStatement:
	"print" "(" toPrint = STRING ")"
;



// Component
EnvInitValue returns Expression:
	{NegativeIntConstant} "-" negvalue=INT |
	{IntConstant} value=INT |
	{StringConstant} value=STRING |
	{BoolConstant} value=('true'|'false') | 
	{TupleConstant} '['(elem += EnvInitValue (',' elem += EnvInitValue)*)?']'
;

EnvInitOrArg returns Expression:
	EnvInitValue | {EnvironmentArg} arg = [EnvParam]
;

Environment:
	{Environment} "{"(attrs += ID ':=' vals+=EnvInitOrArg (","attrs += ID ':=' vals+=EnvInitOrArg)*)?"}"
;

EnvParam : name = ID;

EnvironmentDefinition:
	'environment' name=ID ("(" envParams += EnvParam ("," envParams += EnvParam)*")")? env = Environment
;

ComponentDefinition:
	"component" (envref = [EnvironmentDefinition]("("envargs += EnvInitOrArg(","envargs += EnvInitOrArg)*")")?|env = Environment) ':' block = (PDPBlock|ProcessBlock)
;

// Expression
Expression: Or;

Or returns Expression:
	And ({Or.sub+=current} "||" sub+=And)*
;

And returns Expression:
	Equality ({And.sub+=current} "&&" sub+=Equality)*
;

Equality returns Expression:
	Comparison (
		{Equality.left=current} op=("=="|"!=")
		right=Comparison
	)?
;

Comparison returns Expression:
	PlusOrMinus (
		{Comparison.left=current} op=(">="|"<="|">"|"<")
		right=PlusOrMinus
	)?
;

PlusOrMinus returns Expression:
	MulOrDiv (
		({Plus.left=current} '+' | {Minus.left=current} '-' | {Concatenate.left=current} '++') 
		right=MulOrDiv
	)?
;

MulOrDiv returns Expression:
	Primary (
		{MulOrDiv.left=current} op=('*'|'/'|'%') 
		right=Primary
	)?
;

Primary returns Expression:
	{UnaryMinus} "-" expression=ContainmentExpr |
	{Not} "!" expression=ContainmentExpr |
	ContainmentExpr
;

ContainmentExpr returns Expression:
	AtomicOrExpr ({ContainmentExpression.elem = current} "in" tuple = AtomicOrExpr)?
;

AtomicOrExpr returns Expression:
	'(' Expression ')' | Atomic
;

LRef:
	FuncParam|FuncVarDeclaration
;

Atomic returns Expression:
	{IntConstant} value=INT |
	{StringConstant} value=STRING |
	{BoolConstant} value=('true'|'false') |
	AtomicIndexable ('['{TupleGet.elem = current} idx = Expression ']')?
;

AtomicIndexable returns Expression:
	{TupleConstant} '['(elem += Expression (',' elem += Expression)*)?']'|
	{TupleLength} 'length''('elem = Atomic')'|
	{LocalVarRef} ref=[LRef] |
	{LocalAttributeRef} "proc" "." attribute=ID |
	{FunctionCall} function=[FuncDefinition]"("(params+=Expression(","params+=Expression)*)?")"|
	{ComponentAttributeRef} "comp""."attribute=ID
;

terminal TYPE:
	('int'|'string'|'bool'|'tuple')
;

// OutputPredicate
OutputPredicate returns Expression: OutOr;

OutOr returns Expression:
	OutAnd ({Or.sub+=current} "||" sub+=OutAnd)*
;

OutAnd returns Expression:
	OutEqualityComparison ({And.sub+=current} "&&" sub+=OutEqualityComparison)*
;

OutEqualityComparison returns Expression:
	OutPrimary | 
	( 
		OutAtomicOrRec
		(({OutEqualityComparison.left = current} op=("=="|"!="|">="|"<="|">"|"<") right=OutAtomicOrRec) |
		({ContainmentExpression.elem = current} "in" tuple=OutAtomicOrRec))
	)
;

OutPrimary returns Expression:
	{Not} "!" expression=(OutPrimary|OCE) |
	'(' OutputPredicate ')' | 
	{BoolConstant} value=('true'|'false') 
;

OCE returns Expression:
	{ContainmentExpression} elem = OutAtomicOrRec "in" tuple=OutAtomicOrRec
;

OutAtomicOrRec returns Expression:
	({RecAttributeRef} "receiver""."attribute=ID) |
	OutAtomic
;

OutAtomic returns Expression:
	{IntConstant} value=INT |
	{StringConstant} value=STRING |
	{BoolConstant} value=('true'|'false') |
	OutAtomicIndexable ('['{TupleGet.elem = current} idx = OutAtomic ']')?
;

OutAtomicIndexable returns Expression:
	{TupleConstant} '['(elem += OutAtomic (',' elem += OutAtomic)*)?']'|
	{TupleLength} 'length''('elem = OutAtomic')'|
	{FunctionCall} function=[FuncDefinition]"("(params+=OutAtomic(","params+=OutAtomic)*)?")"|
	{LocalAttributeRef} "proc" "." attribute=ID |
	{ComponentAttributeRef} "comp""."attribute=ID
;

// Functions
FuncParam:
	type = TYPE name = ID
;
FuncDefinition:
	'function' type=TYPE name=ID '('(params+=FuncParam (',' params+=FuncParam)*)?')' blk = FuncBlock
;
FuncBlock:
	'{' {FuncBlock} (statements += FuncStatement)* '}'
;
FuncStatement:
	(FuncVarAssign | FuncIfElse | FuncReturn | FuncVarDeclaration | FuncVarAppend | FuncVarPop | FuncWhile) 
;

FuncVarDeclaration:
	'var' name = ID '=' val = Expression
;

FuncVarAssign:
	var = [FuncVarDeclaration] ('['idx = Expression']')? '=' val = Expression
;

FuncVarAppend:
	var = [FuncVarDeclaration] '.''append''('item = Expression')'
;

FuncVarPop:
	var = [FuncVarDeclaration] '.''pop''('')'
;

FuncIfElse:
	'if' '('test += Expression')' then += FuncBlock
	('else' 'if' '('test += Expression')' then += FuncBlock)*
	('else' elseBranch = FuncBlock)?
;
FuncReturn:
	'return' val = Expression
;

FuncWhile:
	'while''('cond = Expression')' cycle=FuncBlock
;

//@Override 
//terminal INT returns ecore::EInt: ('-')?('0'..'9')+;